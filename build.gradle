//
// Install the gradle Ice Builder plug-in from the plug-in portal
//
plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.5.0' apply false
}
  
subprojects {
    //
    // Apply Java and Ice Builder plug-ins to all sub-projects
    //
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'
 
    //
    // Set Java compatibility to 11 for broader compatibility
    //
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    //
    // Both Client and Server projects share the MIO.ice Slice definitions
    //
    slice {
        java {
            files = [file("../MIO.ice")]
        }
    }
 
    //
    // Use Ice JAR files from maven central repository
    //
    repositories {
        mavenCentral()
    }
 
    //
    // Both Client and Server depend only on Ice JAR
    //
    dependencies {
        implementation 'com.zeroc:ice:3.7.6'
    }
 
    //
    // Create a Fat JAR with all dependencies included
    //
    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        
        manifest {
            attributes(
                "Main-Class": project.name == "server" ? "Server" : "Client"
            )
        }
        
        // Include all dependencies in the JAR
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
    
    // Create worker-client.jar for distributed processing
    if (project.name == "client") {
        task workerJar(type: Jar) {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            archiveBaseName = 'worker-client'
            
            manifest {
                attributes("Main-Class": "WorkerClient")
            }
            
            from sourceSets.main.output
            from {
                configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }
        
        build.dependsOn workerJar
    }
}
